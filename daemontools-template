#!/usr/bin/env perl
use strict;
use warnings;
use utf8;
use Data::Section::Simple qw/get_data_section/;
use Text::Xslate;
use Path::Tiny qw/path/;
use Getopt::Long;

my $dir = shift(@ARGV) or die 'no directory is specified';
GetOptions(\my %opt, qw/
    user=s
/);
$opt{user} ||= 'app';

my $tx = Text::Xslate->new(
    path => [ get_data_section ],
);

path($dir, 'log')->mkpath;

my $run = path($dir, 'run');
$run->spew_utf8($tx->render('run.tx', {user => $opt{user}}));
chmod 0755, $run;

my $log_run = path($dir, 'log', 'run');
$log_run->spew_utf8($tx->render('log_run.tx', {user => $opt{user}}));
chmod 0755, $log_run;

__DATA__
@@ run.tx
#!/bin/sh
exec 2>&1
exec setuidgid <: $user :> /path/to/command

@@ log_run.tx
#!/bin/sh
exec setuidgid <: $user :> multilog t n10 s16777215 .
